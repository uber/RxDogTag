/*
 * Copyright (C) 2019. Uber Technologies
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

pluginManagement {
  apply from: file('gradle/dependencies.gradle')
  resolutionStrategy {
    eachPlugin {
      switch (requested.id.id) {
        case 'me.champeau.gradle.jmh':
          useVersion(deps.versions.jmhPlugin)
          break
        case 'org.jetbrains.dokka':
          useModule("org.jetbrains.dokka:dokka-gradle-plugin:${deps.versions.dokka}")
          break
        case 'net.ltgt.errorprone':
          useVersion(deps.versions.errorPronePlugin)
          break
        case 'net.ltgt.nullaway':
          useVersion(deps.versions.nullawayPlugin)
          break
        case 'ru.vyarus.animalsniffer':
          useVersion(deps.versions.animalSniffer)
          break
      }
    }
  }
  repositories {
    mavenCentral()
    gradlePluginPortal()
    jcenter()
  }
}

rootProject.name = 'rxdogtag-root'
if (System.getenv("ANDROID_HOME") != null) {
  include ':android-benchmark:benchmark'
}
include ':rxdogtag'
include ':rxdogtag-autodispose'
