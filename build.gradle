/*
 * Copyright (C) 2019. Uber Technologies
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import net.ltgt.gradle.errorprone.CheckSeverity
buildscript {
  apply from: rootProject.file('gradle/dependencies.gradle')
  buildscript {
    repositories {
      google()
      mavenCentral()
      gradlePluginPortal()
      jcenter()
    }
    dependencies {
      classpath "com.android.tools.build:gradle:3.5.0-beta01"
      classpath "androidx.benchmark:benchmark-gradle-plugin:1.0.0-alpha01"
      classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.31"
      classpath "net.ltgt.gradle:gradle-errorprone-plugin:${deps.versions.errorPronePlugin}"
      classpath "net.ltgt.gradle:gradle-nullaway-plugin:${deps.versions.nullawayPlugin}"
      classpath "ru.vyarus:gradle-animalsniffer-plugin:${deps.versions.animalSniffer}"
      classpath "com.diffplug.spotless:spotless-plugin-gradle:${deps.versions.spotless}"
      classpath 'com.vanniktech:gradle-maven-publish-plugin:0.8.0'
    }
  }
}

subprojects {
  repositories {
    google()
    mavenCentral()
    jcenter()
  }

  apply from: rootProject.file('gradle/dependencies.gradle')
  apply plugin: 'com.diffplug.gradle.spotless'
  spotless {
    format 'misc', {
      target '**/*.md', '**/.gitignore'

      indentWithTabs()
      trimTrailingWhitespace()
      endWithNewline()
    }
    java {
      target "**/*.java"
      googleJavaFormat(deps.versions.gjf)
      licenseHeaderFile rootProject.file('spotless/copyright.java')
      removeUnusedImports()
      trimTrailingWhitespace()
      endWithNewline()
    }
    groovyGradle {
      target '**/*.gradle'
      trimTrailingWhitespace()
      endWithNewline()
    }
  }

  if (!project.name.contains("benchmark")) {
    apply plugin: 'java-library'

    sourceCompatibility = deps.build.javaVersion
    targetCompatibility = deps.build.javaVersion
    test {
      testLogging.showStandardStreams = true
      maxParallelForks Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    }
    apply plugin: 'net.ltgt.errorprone'
    apply plugin: 'net.ltgt.nullaway'
    dependencies {
      errorproneJavac deps.build.errorProneJavac
      errorprone deps.build.nullAway
      errorprone deps.build.errorProne
    }
    tasks.withType(JavaCompile).configureEach {
      options.errorprone.nullaway {
        severity = CheckSeverity.ERROR
        annotatedPackages.add("com.uber")
      }
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

apply from: 'gradle/dependencies.gradle'
apply from: 'gradle/publish-docs.gradle'
