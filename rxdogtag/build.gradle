/*
 * Copyright (C) 2019. Uber Technologies
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'ru.vyarus.animalsniffer'
  id 'me.champeau.gradle.jmh'
  id 'org.jetbrains.dokka'
}

dependencies {
  api deps.rx.java

  signature deps.build.animalSniffer
  testImplementation deps.test.junit
  testImplementation deps.test.truth
}

dokka {
  externalDocumentationLink {
    url = new URL("http://reactivex.io/RxJava/3.x/javadoc/")
  }

  // Output as Github flavored markdown so that docs show up inline in mkdocs website
  outputFormat = 'gfm'
  outputDirectory = "$rootDir/docs/2.x"
  classpath = new ArrayList<File>(project.tasks['assemble'].outputs.files.files)
  sourceDirs = files('src/main/java')
}

// We setup jmh such that you must provide the `-Pjmh` to specify which tests
// to run. If it has no value then all tests are run, but you can specify
// tests by providing a value. (e.g., if you want to run "SubscriptionPerf",
// you can run `./gradlew jmh -Pjmh=SubscriptionPerf`)
jmh {
  jmhVersion = deps.versions.jmhLib

  if (project.hasProperty("jmh")) {
    include = [".*" + project.jmh + ".*"]
    println("JMH: " + include)
  }
}

apply plugin: 'com.vanniktech.maven.publish'
